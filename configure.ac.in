# vi:set et ai sw=2 sts=2 ts=2: */
#-
# Copyright (c) 2004-2007 Benedikt Meurer <benny@expidus.org>
# Copyright (c) 2009-2011 Jannis Pohlmann <jannis@expidus.org>
# Copyright (c) 2004-2015 Expidus Development Team <expidus1-dev@expidus.org>
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License as
# published by the Free Software Foundation; either version 2 of
# the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public
# License along with this program; if not, write to the Free
# Software Foundation, Inc., 51 Franklin Street, Fifth Floor,
# Boston, MA 02110-1301, USA.

dnl ***************************
dnl *** Version information ***
dnl ***************************
m4_define([lunarx_verinfo], [0:0:0])
m4_define([lunarx_version_api], [1])
m4_define([lunar_version_major], [1])
m4_define([lunar_version_minor], [0])
m4_define([lunar_version_micro], [0])
m4_define([lunar_version_nano], [])
m4_define([lunar_version_build], [@REVISION@])
m4_define([lunar_version_tag], [git])
m4_define([lunar_version], [lunar_version_major().lunar_version_minor().lunar_version_micro()ifelse(lunar_version_nano(), [], [], [.lunar_version_nano()])ifelse(lunar_version_tag(), [git], [lunar_version_tag()-lunar_version_build()], [lunar_version_tag()])])

dnl *******************************************
dnl *** Debugging support for GIT snapshots ***
dnl *******************************************
m4_define([lunar_debug_default], [ifelse(lunar_version_tag(), [git], [yes], [minimum])])

dnl ***************************
dnl *** Initialize autoconf ***
dnl ***************************
AC_COPYRIGHT([Copyright (c) 2004-2020 The Lunar development team. All rights reserved.])
AC_INIT([lunar], [lunar_version], [https://gitlab.expidus.com/expidus/lunar], [lunar])
AC_PREREQ([2.60])
AC_CONFIG_MACRO_DIRS([m4])
AC_CANONICAL_TARGET()
AC_REVISION([lunar_version_build])

dnl ***************************
dnl *** Initialize automake ***
dnl ***************************
AM_INIT_AUTOMAKE([1.11 dist-bzip2 tar-ustar no-dist-gzip foreign])
AC_CONFIG_HEADERS([config.h])
AM_MAINTAINER_MODE()
m4_ifdef([AM_SILENT_RULES], [AM_SILENT_RULES([yes])])

dnl *******************************
dnl *** Check for UNIX variants ***
dnl *******************************
AC_AIX()
AC_ISC_POSIX()
AC_MINIX()

if test "x$target_os" = "xlinux-gnu"; then
  AC_DEFINE([HAVE_LINUX], [], [Whether we are building on Linux or not])
fi

dnl ********************************
dnl *** Check for basic programs ***
dnl ********************************
AC_PROG_CC()
AC_PROG_LD()
AM_PROG_CC_C_O()
AC_PROG_INSTALL()
IT_PROG_INTLTOOL()
AC_CHECK_PROGS([PERL], [perl5 perl])
m4_ifdef([AC_PROG_SED], [AC_PROG_SED], [AC_CHECK_PROG(SED, sed, sed)])

dnl **************************
dnl *** Initialize libtool ***
dnl **************************
LT_PREREQ([2.4])
LT_INIT([disable-static])

dnl **************************************
dnl *** Substitute version information ***
dnl **************************************
LUNARX_VERINFO=lunarx_verinfo()
LUNARX_VERSION_API=lunarx_version_api()
LUNAR_VERSION_MAJOR=lunar_version_major()
LUNAR_VERSION_MINOR=lunar_version_minor()
LUNAR_VERSION_MICRO=lunar_version_micro()
AC_SUBST([LUNARX_VERINFO])
AC_SUBST([LUNARX_VERSION_API])
AC_SUBST([LUNAR_VERSION_MAJOR])
AC_SUBST([LUNAR_VERSION_MINOR])
AC_SUBST([LUNAR_VERSION_MICRO])

dnl ******************************
dnl *** Set helper path prefix ***
dnl ******************************
AC_ARG_WITH([helper-path-prefix],
            [AC_HELP_STRING([--with-helper-path-prefix=PATH],
                            [Path prefix under which helper executables will be installed (default: $libdir)])],
            [HELPER_PATH_PREFIX="$withval"],
            [HELPER_PATH_PREFIX="$libdir"])
AC_SUBST([HELPER_PATH_PREFIX])

dnl ***********************************************
dnl *** Work-around system-specific limitations ***
dnl ***********************************************
AC_SYS_LARGEFILE()

dnl **********************************
dnl *** Check for standard headers ***
dnl **********************************
AC_CHECK_HEADERS([ctype.h errno.h fcntl.h grp.h limits.h locale.h memory.h \
                  paths.h pwd.h sched.h signal.h stdarg.h stdlib.h string.h \
                  sys/mman.h sys/param.h sys/stat.h sys/time.h sys/types.h \
                  sys/uio.h sys/wait.h time.h])

dnl ************************************
dnl *** Check for standard functions ***
dnl ************************************
AC_FUNC_MMAP()
AC_CHECK_FUNCS([localeconv mkdtemp pread pwrite sched_yield setgroupent \
                setpassent strcoll strlcpy strptime symlink atexit])

dnl ******************************
dnl *** Check for i18n support ***
dnl ******************************
XDT_I18N([@LINGUAS@])

dnl *********************
dnl *** Check for X11 ***
dnl *********************
XDT_CHECK_LIBX11_REQUIRE()

dnl *************************
dnl *** Check for gtk-doc ***
dnl *************************
GTK_DOC_CHECK([1.9])

dnl ***********************************
dnl *** Check for required packages ***
dnl ***********************************
XDT_CHECK_PACKAGE([ENDO], [endo-2], [1.0.0])
XDT_CHECK_PACKAGE([GLIB], [glib-2.0], [2.50.0])
XDT_CHECK_PACKAGE([GIO], [gio-2.0], [2.50.0])
XDT_CHECK_PACKAGE([GTHREAD], [gthread-2.0], [2.50.0])
XDT_CHECK_PACKAGE([GMODULE], [gmodule-2.0], [2.50.0])
XDT_CHECK_PACKAGE([GTK], [gtk+-3.0], [3.22.0])
XDT_CHECK_PACKAGE([GDK_PIXBUF], [gdk-pixbuf-2.0], [2.14.0])
XDT_CHECK_PACKAGE([LIBEXPIDUS1UTIL], [libexpidus1util-1.0], [1.0.0])
XDT_CHECK_PACKAGE([LIBEXPIDUS1UI], [libexpidus1ui-2], [1.0.0])
XDT_CHECK_PACKAGE([LIBEXPIDUS1KBD_PRIVATE], [libexpidus1kbd-private-3], [1.0.0])
XDT_CHECK_PACKAGE([ESCONF], [libesconf-0], [4.12.0])
XDT_CHECK_PACKAGE([PANGO], [pango], [1.38.0])

AC_DEFINE(GLIB_VERSION_MIN_REQUIRED, GLIB_VERSION_2_50, [Ignore post 2.50 deprecations])
AC_DEFINE(GLIB_VERSION_MAX_ALLOWED, GLIB_VERSION_2_50, [Prevent post 2.50 APIs])

dnl ******************************
dnl *** GObject Instrospection ***
dnl ******************************
GOBJECT_INTROSPECTION_CHECK([1.30.0])

dnl ********************************************
dnl *** Check for session management support ***
dnl ********************************************
XDT_CHECK_LIBSM()

dnl **********************************
dnl *** Optional GIO UNIX features ***
dnl **********************************
XDT_CHECK_OPTIONAL_PACKAGE([GIO_UNIX], [gio-unix-2.0],
                           [2.30.0], [gio-unix], [GIO UNIX features])

dnl ***************************************************************
dnl *** Optional support for GUDev (required for lunar-volman) ***
dnl ***************************************************************
XDT_CHECK_OPTIONAL_PACKAGE([GUDEV], [gudev-1.0], [145], [gudev],
                           [GUDev (required for lunar-volman)])

dnl **************************************
dnl *** Optional support for libnotify ***
dnl **************************************
XDT_CHECK_OPTIONAL_PACKAGE([LIBNOTIFY], [libnotify], [0.4.0], [notifications],
                           [Mount notification support], [yes])

dnl ***********************************
dnl *** Check for debugging support ***
dnl ***********************************
XDT_FEATURE_DEBUG([lunar_debug_default])

dnl **************************************
dnl *** Check for linker optimizations ***
dnl **************************************
XDT_FEATURE_LINKER_OPTS()

dnl dnl ****************************************
dnl dnl *** Check for ELF visibility support ***
dnl dnl ****************************************
dnl XDT_FEATURE_VISIBILITY()

dnl *********************************
dnl *** Substitute platform flags ***
dnl *********************************
AC_MSG_CHECKING([PLATFORM_CPPFLAGS])
AC_MSG_RESULT([$PLATFORM_CPPFLAGS])
AC_SUBST([PLATFORM_CPPFLAGS])
AC_MSG_CHECKING([PLATFORM_CFLAGS])
AC_MSG_RESULT([$PLATFORM_CFLAGS])
AC_SUBST([PLATFORM_CFLAGS])
AC_MSG_CHECKING([PLATFORM_LDFLAGS])
AC_MSG_RESULT([$PLATFORM_LDFLAGS])
AC_SUBST([PLATFORM_LDFLAGS])

dnl ************************************
dnl *** Check for additional plugins ***
dnl ************************************
BM_LUNAR_PLUGIN_APR()
BM_LUNAR_PLUGIN_SBR()
BM_LUNAR_PLUGIN_TPA()
BM_LUNAR_PLUGIN_UCA()
BM_LUNAR_PLUGIN_WALLPAPER()

AC_OUTPUT([
Makefile
docs/Makefile
docs/design/Makefile
docs/papers/Makefile
docs/reference/Makefile
docs/reference/lunarx/Makefile
docs/reference/lunarx/version.xml
examples/Makefile
examples/tex-open-terminal/Makefile
icons/Makefile
icons/16x16/Makefile
icons/24x24/Makefile
icons/48x48/Makefile
icons/128x128/Makefile
icons/scalable/Makefile
plugins/Makefile
plugins/lunar-apr/Makefile
plugins/lunar-sbr/Makefile
plugins/lunar-sendto-email/Makefile
plugins/lunar-tpa/Makefile
plugins/lunar-uca/Makefile
plugins/lunar-wallpaper/Makefile
po/Makefile.in
lunar/Makefile
lunarx/Makefile
lunarx/lunarx-3.pc
lunarx/lunarx-config.h
])

dnl ***************************
dnl *** Print configuration ***
dnl ***************************
echo
echo "Build Configuration:"
echo
if test x"$GIO_UNIX_FOUND" = x"yes"; then
echo "* GIO UNIX features:                  yes"
else
echo "* GIO UNIX features:                  no"
fi
if test x"$GUDEV_FOUND" = x"yes"; then
echo "* GUDev (required for lunar-volman): yes"
else
echo "* GUDev (required for lunar-volman): no"
fi
if test x"$LIBNOTIFY_FOUND" = x"yes"; then
echo "* Mount notification support:         yes"
else
echo "* Mount notification support:         no"
fi
echo "* Debug Support:                      $enable_debug"
echo "* GObject Instrospection support:     $enable_introspection"
echo
echo "Additional Plugins:"
echo
echo "* Advanced Properties:                $ac_bm_lunar_plugin_apr"
echo "* Simple Builtin Renamers:            $ac_bm_lunar_plugin_sbr"
echo "* Trash Panel Applet:                 $ac_bm_lunar_plugin_tpa"
echo "* User Customizable Actions:          $ac_bm_lunar_plugin_uca"
echo "* Wallpaper support:                  $ac_bm_lunar_plugin_wallpaper"
echo
